[BIT 32]

; C 언어에서 호출할 수 있도록 이름을 노출함 Export
global readCPUID, switchAndExecute64BitKernel

SECTION.text

readCPUID:
	push ebp      ; 베이스 포인터 레지스터 EBP 를 스택에 삽입
	mov ebp, esp  ; 베이스 포인터 레지스터 EBP 에 스택 포인터 레지스터 ESP 의 값을 설정
	push eax      ; 함수에서 임시로 사용하는 레지스터로 함수의 마지막 부분에서 스택에 삽입된 값을 꺼내 원래 값으로 복원
	push ebx
	push ecx
	push edx
	push esi

	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; EAX 레지스터의 값으로 CPUID 명령어 실행
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	mov eax, dword [ebp + 8] ; 파라미터 1( dwEAX ) 를 EAX 레지스터에 저장
	cpuid			 		 ; CPUID 명령어 실행
	
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; 변환된 값을 파라미터에 저장
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; *pdwEAX
	mov esi, dword [ebp + 12]
	mov dword [esi], eax

	; *pdwEBX
	mov esi, dword [ebp + 16]
	mov dword [esi], ebx

	; *pdwECX
	mov esi, dword [ebp + 20]
	mov dword [esi], ecx

	; *pdwEDX
	mov esi, dword [ebp + 24]
	mov dword [esi], edx

	; 함수에서 사용이 끝난 ESI 레지스터부터 EBP 레지스터까지를스택에 삽입된 값을 이용해서 복원
	pop esi							
	pop edx
	pop ecx
	pop ebx
	pop eax
	pop ebp
	ret

	; IA-32e 모드로 전환하고 64 비트 커널을 수행
switchAndExecute64BitKernel:
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; CR4 컨트롤 레지스터의 PAE 비트를 1 로 설정
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	mov eax, cr4  ; CR4 컨트롤 레지스터의 값을 EAX 레지스터로 저장
	or eax, 0x20  ; PAE 비트( 비트 5 ) 를 1 로 설정
	mov cr4, eax  ; PAE 비트가 1 로 설정된 값을 CR4 컨트롤 레지스터에 저장

	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; CR3 컨트롤 레지스터에 PML4 테이블의 어드레스와 캐시 활성화
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	mov eax, 0x100000 ; EAX 레지스터에 PML4 테이블이 존재하는 0x100000 ( 1MB ) 를 저장
	mov cr3, eax 	  ; CR3 컨트로 ㄹ레지스터에 0ㅌ100000 ( 1MB ) 를 저장

	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; IA32_EFER.LME 를 1 로 설정하여 IA-32e 모드를 활성화
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	mov ecx, 0xc0000080 ; IA32_EFER MSR 레지스터의 어드레스를 저장
	rdmsr				; MSR 레지스터를 읽기

	or eax, 0x0100		; EAX 레지스터에 저장된 IA32_EFER MSR 의 하위 32 비트에서 LME 비트 ( 비트 8 ) 을 1로 설정
	
	wrmsr				; MSR 레지스터에 쓰기

	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; CR0 컨트롤 레지스터를 NW 비트 ( 비트 29 ) = 0, CD 비트 ( 비트 30 ) = 0,
	; PG 비트 ( 비트 313 ) = 1 로 설정하여 캐시 기능과 페이징 기능을 활성화
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	mov eax, cr0
	or eax, 0xE0000000   ; NW 비트 ( 29 ), CD 비트 ( 30 ), PG 비트 ( 31 ) 을 모두 1 로 설정
	xor eax, 0x60000000  ; NW 비트 ( 29 ), CD 비트 ( 30 ), XOR 하여 0 으로 설정PG 비트 ( 31 ) 을 모두 1 로 설정	
	mov cr0, eax		 ; NW 비트 = 0, CD 비트 = 0, PG 비트 = 1 로 설정한 값ㅇ르 다시 CR0 컨트롤 레지스터에 저장

	jmp 0x08:0x200000   ; CS 세그먼트 셀렉터를 IA-32e 모드용 코드 세그먼트 디스크립터로 
						; 교체하고 0x200000 ( 2MB ) 어드레스로 이동

	; 여기는 실행되지 않음
	jmp $


